 (filenames snippetNames indices names msg)←{all}CollectSnippetInfo y;caption;txt;sourceFile;folder;ind2;bool;list2;multiple
 all←{0<⎕NC ⍵:⍎⍵ ⋄ 1}'all'
 (caption txt sourceFile multiple)←4↑y,1
 snippetNames←msg←''
 folder←GetSnippetsFolder
 filenames←FilesAndDirs.ListFiles folder
 :If 0=≢filenames
     msg←'No ',((~all)/'fixable'),' snippets found' ⋄ →0
 :EndIf
 :If all
     bool←1
 :Else
     bool←'.code'∘≢¨3⊃∘⎕NPARTS¨filenames
     filenames←bool/filenames
 :EndIf
 :If 0=≢filenames
     msg←'No ',((~all)/'fixable'),' snippets found' ⋄ →0
 :EndIf
 snippetNames←2⊃∘⎕NPARTS¨filenames
 names←''
 :If 0=≢txt
     txt←'*'
 :EndIf
 :If '*'∊names←txt
     indices←⍸(⎕C names~'*'){⍺∘≡¨⎕C(≢⍺)↑¨⍵}snippetNames
     :If 0=≢indices
         msg←'No match found' ⋄ names←'' ⋄ →0
     :EndIf
     :If 1=≢indices
         names←indices⊃snippetNames
     :Else
         list2←1↓↓bool{(1,(¯1+≢⍵)⍴⍺)⌿⍵}sourceFile List_ ns
         ind2←('SelectSnippets@Select snippet',(multiple/'s'),' ',caption)multiple CommTools.Select list2
         :If 0=≢ind2
             names←''
             msg←'Cancelled by user'
         :Else
             indices←indices[ind2]
             :If multiple
                 names←snippetNames[indices]
             :Else
                 names←indices⊃snippetNames
             :EndIf
         :EndIf
     :EndIf
 :Else
     :If ','∊names
         names←','(≠⊆⊢)names
     :EndIf
     names←⊆names
     names~¨←⊂''' '
     indices←⍸(⎕C snippetNames)∊⎕C names
     :If 0=≢indices
         msg←'No snippet found'
         names←''
     :Else
         names←snippetNames[indices]
     :EndIf
 :EndIf
