 msg←sourceFile Fix_ ns;filenames;snippetNames;indices;names;parent;name;ind;caption;filename;i
 (filenames snippetNames indices names msg)←0 CollectSnippetInfo'to fix in the workspace'({0≡⍵:'' ⋄ ⍵}ns._1)sourceFile 1
 :If 'Cancelled by user'≢msg
     parent←''ns.Switch'target'
     :If 0<≢names
         :If 0=≢parent
             :If (,'#')≢parent←Args.CalledFrom
                 caption←'WhereToFix@Where do you want to fix ',((1+1<≢indices)⊃'it' 'them'),':'
                 ind←caption CommTools.Select(⊂,'#'),⊂parent
                 :If 0=≢ind
                     msg←'Cancelled by user' ⋄ r←1 ⋄ →0
                 :EndIf
                 parent←⍎ind⊃'#'parent
             :Else
                 parent←#
             :EndIf
         :Else
             parent←⍎parent
         :EndIf
         :For i :In ⍳≢indices
             name←i⊃,names
             filename←(i⊃,indices)⊃filenames
             msg←Fix2 name parent filename
             :If 'Cancelled by user'≡msg
                 :If (i+1)≤≢indices
                 :AndIf 1 CommTools.YesOrNo'Quit@Continue with the next snippet?'
                     :Continue
                 :Else
                     :Return
                 :EndIf
             :Else
                 ⎕←msg
                 msg←''
             :EndIf
         :EndFor
     :Else
         msg←'No such (fixable) snippet found'
     :EndIf
 :EndIf
