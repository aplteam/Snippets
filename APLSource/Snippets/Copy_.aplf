 msg←Copy_ ns;name;ind;cl;success;filenames;snippetNames;counter;max;qdmx
 :If 'Win'≡3↑1⊃'#'⎕WG'APLVersion'
     name←{0≡⍵:'' ⋄ ⍵}ns._1
     (filenames snippetNames ind name msg)←CollectSnippetInfo'to copy to clipboard'name 0
     :If 0=≢msg
         success←counter←0
         max←5
         :Repeat
             :Trap 0 ⍝ Yes, this fails occasionally under Windows for no reason at all
                 'cl'⎕WC'Clipboard'
                 cl{11::⍺{_←⎕DL 0.5 ⋄ ⍺ ∇ ⍵}⍵ ⋄ ⍺.Text←⍵}GetCodeFromFile ind⊃filenames
                 success←1
             :Else
                 ⎕DL 0.01
                 counter+←1
             :EndTrap
         :Until success∨counter>max
         qdmx←⎕DMX
         ('Could not load <',(⊃,/1 ⎕NPARTS ind⊃filenames),'> : ',qdmx.Message)Assert success
         msg←'Copied to the clipboard: ',ind⊃snippetNames
     :EndIf
 :Else
     msg←'This command is only available on Windows'
 :EndIf
