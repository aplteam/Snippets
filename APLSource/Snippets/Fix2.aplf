 msg←Fix2(name parent filename);index;body;extension;isVars;qdmx;name_;q;buff;res
 body←GetCodeFromFile filename
 extension←3⊃⎕NPARTS filename
 :If isVars←'.apla'≡extension
     name←2⊃⎕NPARTS filename
 :Else
     body←{2=⍴⍴⍵:⍵ ⋄ ,⊆⍵}body
     :Trap 0
         name←GetNameFromBody body
     :Else
         qdmx←⎕DMX
         qdmx.EM ⎕SIGNAL qdmx.EN
     :EndTrap
     :If name≢2⊃⎕NPARTS filename
     :AndIf ~1 CommTools.YesOrNo'DifferentName@<',({2⊃⎕NPARTS ⍵}filename),'> will be fixed as "',name,'"; you okay with that?'
         msg←'Cancelled by user' ⋄ →0
     :EndIf
 :EndIf
 :If '-'∊name
     name_←{⍵↑⍨1+-'-'⍳⍨⌽⍵}name
 :Else
     name_←name
 :EndIf
 :Select ⊃parent.⎕NC name_
 :Case 0
              ⍝ Name is okay and not in use, nothing to do
 :Case ¯1
     ('Invalid name: ',name_)⎕SIGNAL 11
 :Else
     q←'"',name_,'" already defined in ',(⍕parent),'; overwrite?'
     :If 0=1 CommTools.YesOrNo q
         msg←'Cancelled by user' ⋄ →0
     :EndIf
     parent.⎕EX name_
 :EndSelect
 :If (⊂extension)∊'.aplf' '.aplo'
     buff←parent.⎕FX body
     :If ' '=1↑0⍴∊buff
         msg←'Function "',name_,'" fixed in ',⍕parent
     :Else
         msg←'Attempt to fix "',name_,'" caused an error in line ',⍕buff
     :EndIf
     res←⎕SE.Link.Add(⍕parent),'.',name_
     :If 'Added:'{⍺≡(≢⍺)↑⍵}res
         msg,←'; ',res
     :EndIf
 :ElseIf isVars
     ⍎(⍕parent),'.',name_,'←body'
     msg←'Variable "',name_,'" assigned in "',(⍕parent),'"'
 :Else
     :Trap 0
         {}parent.⎕FIX body
         msg←'Script "',name_,'" fixed in ',⍕parent
     :Else
         msg←'Attempt to fix "',name_,'" caused an error'
     :EndTrap
 :EndIf
