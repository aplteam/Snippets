 r←sourceFile Show_ ns;opCode;msg;name;filename;session;ref;body
 r←''
 (opCode r name filename)←'SelectSnippetForView@Please select snippet to be shown with ⎕ED'GetNameAndFilename ns
 :If 1=opCode
     session←ns.Switch'session'
     :If session
         r←⊃FilesAndDirs.NGET filename 1
         :If '.apla'≡3⊃⎕NPARTS filename
             r←⎕SE.Dyalog.Array.Deserialise r
         :EndIf
         r←⍪↓APLTreeUtils2.DLB↑r
         r←(⊂(⎕PW-5)⍴'-')⍪r
     :Else
         (name body)←2↑GetNameAndBody filename
         :Select ⎕C 3⊃⎕NPARTS filename
         :Case '.apla'
             ref←name EstablishVarsInNamespace body
         :Case '.code'
             ref←name EstablishCodeSnippetInNamespace body
         :Else
             ref←EstablishCodeInNamespace body
         :EndSelect
         ref.{{}(⎕ED⍠('ReadOnly' 1)&⍵)}name
     :EndIf
 :EndIf
